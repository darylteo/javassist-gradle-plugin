buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
  }

  dependencies {
    classpath 'com.darylteo.gradle:javassist-plugin:0.3.0'
  }
}

import com.darylteo.gradle.javassist.tasks.TransformationTask
import com.darylteo.gradle.javassist.transformers.ClassTransformer
import javassist.CtClass

apply plugin: 'java'

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.+'
}

class MyTransformation extends ClassTransformer {
  public void applyTransformations(CtClass clazz) throws Exception {
    clazz.setName("test.package." + clazz.getName());
  }

  public boolean shouldFilter(CtClass clazz) {
    return clazz.getName().equals("MyClass2");
  }
}

// transformation empty
task transform1(type: TransformationTask) {
  from sourceSets.main.output
  into("${buildDir}/transforms/transform1")
}

// transformation
task transform2(type: TransformationTask) {
  from sourceSets.main.output

  transform { c ->
    c.name = "test.package.${c.name}"
  }
}

// transformation with filter
task transform3(type: TransformationTask) {
  from sourceSets.main.output

  where { c ->
    c.name == 'MyClass2'
  }.transform { c ->
    c.name = "test.package.${c.name}"
  }
}

// transformation with transformation class
task transform4(type: TransformationTask) {
  classpath += configurations.compile

  from sourceSets.main.output

  transformation = new MyTransformation()
}

task testTransforms {
  dependsOn transform1
  dependsOn transform2
  dependsOn transform3
  dependsOn transform4
}