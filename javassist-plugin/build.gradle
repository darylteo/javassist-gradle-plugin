buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.0'
  }
}

apply from: rootProject.file('gradle/plugin.gradle')

apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

version = '0.4.0'

dependencies {
  compile 'org.javassist:javassist:3.18.2-GA'
  compile 'com.github.stephanenicolas.javassist:javassist-build-plugin-api:1.0.0'
}

def info = {
  project {
    name 'Javassist Gradle Plugin'
    description 'Plugin Task that facilitates Javassist bytecode generation'
    inceptionYear '2013'

    developers {
      developer {
        id 'darylteo'
        name 'Daryl Teo'
        email 'i.am@darylteo.com'
        url 'http://darylteo.com'
        roles {
          role 'owner'
        }
      }

      developer {
        id 'stephanenicolas'
        name 'St√©phane Nicolas'
        url 'https://github.com/stephanenicolas'
        roles {
          role 'contributor'
        }
      }
    }
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      pom info
    }
  }
}

install {
  repositories {
    mavenInstaller {
      pom info
    }
  }
}

test {
  workingDir = project.rootDir
}

jacocoTestReport {
  reports {
    xml.enabled = true // coveralls plugin depends on xml format report
    html.enabled = true
  }
}